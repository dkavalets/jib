plugins {
  id 'java-gradle-plugin'
  id 'net.researchgate.release'
  id 'com.gradle.plugin-publish'
}

repositories {
  // because gradle plugin dependencies are pulling from jcenter
  jcenter()
}

dependencies {
  // for gradle plugin development
  implementation gradleApi()

  implementation 'com.google.http-client:google-http-client'
  implementation 'com.fasterxml.jackson.core:jackson-databind'
  implementation 'com.google.guava:guava'
  sourceProject project(":jib-core")
  sourceProject project(":jib-plugins-common")

  testImplementation 'junit:junit'
  testImplementation 'org.mockito:mockito-core'
  testImplementation 'org.slf4j:slf4j-api'
  testImplementation project(path:':jib-plugins-common', configuration:'tests')
  integrationTestImplementation project(path:':jib-core', configuration:'integrationTests')

  // only for testing a concrete Spring Boot example in a test (not for test infrastructure)
  testImplementation 'org.springframework.boot:spring-boot-gradle-plugin:2.1.6.RELEASE'
}

/* JAR */
// Necessary for adding version information into the JAR.
jar {
  manifest {
    attributes 'Implementation-Title': project.name,
               'Implementation-Version': version,
               'Built-By': System.getProperty('user.name'),
               'Built-Date': new Date(),
               'Built-JDK': System.getProperty('java.version'),
               'Built-Gradle': gradle.gradleVersion
  }
}
/* JAR */

/* RELEASE */
// Prepare release
release {
  tagTemplate = 'v$version-gradle'
  git {
    requireBranch = /^gradle_release_v\d+.*$/  //regex
  }
}
// Gradle Plugin Portal releases
pluginBundle {
  website = 'https://github.com/GoogleContainerTools/jib/'
  vcsUrl = 'https://github.com/GoogleContainerTools/jib/'

  plugins {
    jibPlugin {
      id = 'com.google.cloud.tools.jib'
      displayName = 'Jib'
      description = 'Containerize your Java application'
      tags = ['google', 'java', 'containers', 'docker', 'kubernetes', 'microservices']
    }
  }
}
tasks.publishPlugins.dependsOn integrationTest
/* RELEASE */
